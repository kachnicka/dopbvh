#version 460

#extension GL_EXT_buffer_reference2: require
#extension GL_EXT_scalar_block_layout: require
#extension GL_EXT_shader_explicit_arithmetic_types_int32: require
#extension GL_EXT_shader_explicit_arithmetic_types_int64: require

#extension GL_KHR_memory_scope_semantics : require
#extension GL_KHR_shader_subgroup_basic : require
#extension GL_KHR_shader_subgroup_ballot : require
#extension GL_KHR_shader_subgroup_arithmetic : require

layout(local_size_x_id = 0) in;
layout(constant_id = 1) const uint32_t SC_subgroupSize = 32u;

#extension GL_GOOGLE_include_directive : enable

#define INCLUDE_FROM_SHADER
#include "data_bvh.h"
#include "data_plocpp.h"

#include "bv_aabb.glsl"
#include "bv_dop14.glsl"
#include "bv_obb.glsl"

float bvArea(vec3[14] points)
{
    Aabb aabb = Aabb(vec3(points[0].x, points[2].y, points[4].z), vec3(points[1].x, points[3].y, points[5].z));

    OBB obb = OBB(vec3(1.f, 0.f, 0.f), vec3(0.f, 1.f, 0.f), vec3(0.f, 0.f, 1.f), aabb.min, aabb.max);
    obbByDiTO14(obb, points);

    vec3 d = (obb.max - obb.min) * vec3(length(obb.b0), length(obb.b1), length(obb.b2));
    return 2.f * (d.x * d.y + d.x * d.z + d.z * d.y);
}


layout (push_constant) uniform uPushConstant{
    uint64_t bvhAddress;
    uint64_t bvhTrianglesAddress;
    uint64_t bvhTriangleIndicesAddress;
    uint64_t bvhCollapsedAddress;
    uint64_t bvhCollapsedTrianglesAddress;
    uint64_t bvhCollapsedTriangleIndicesAddress;

    uint64_t counterAddress;
    uint64_t nodeStateAddress;
    uint64_t sahCostAddress;
    uint64_t leafNodeAddress;
    uint64_t newNodeIdAddress;
    uint64_t newTriIdAddress;
    uint64_t collapsedTreeNodeCountsAddress;

    uint64_t schedulerDataAddress;
    uint64_t indirectDispatchBufferAddress;

    uint64_t debugAddress;

    uint32_t leafNodeCount;
    uint32_t maxLeafSize;

    float c_t;
    float c_i;
} pc;

#include "taskScheduler.glsl"

#define INVALID_ID -1

#define COLLAPSING_SIZE_THRESHOLD 15
#define NODE_STATE_SUBTREE 69
#define NODE_STATE_LEAF 420
#define NODE_STATE_INVALID -1

const uint32_t SC_subgroupCount = gl_WorkGroupSize.x / SC_subgroupSize;

shared uint32_t sharedPrefix;
shared uint32_t aggregatePerSubgroup[SC_subgroupCount];

uint divCeil(in uint a, in uint b)
{
    return (a + b - 1) / b;
}

void decideLeafOrInternal(in uint32_t taskId)
{
    uint32_t nodeId = taskId * gl_WorkGroupSize.x + gl_LocalInvocationID.x;
    if (nodeId >= pc.leafNodeCount)
        return;

    u32Buf counter = u32Buf(pc.counterAddress);
    u32Buf nodeState = u32Buf(pc.nodeStateAddress);
    u32Buf sahCost = u32Buf(pc.sahCostAddress);
    BvhBinaryDiTO14Points bvh = BvhBinaryDiTO14Points(pc.bvhAddress);

    NodeBvhBinaryDiTO14Points node = bvh.node[nodeId];
    float nodeSurfaceArea = bvArea(node.bv);
    // leaf node size is always 1
    sahCost.val[nodeId] = floatBitsToUint(nodeSurfaceArea * pc.c_i);
    nodeState.val[nodeId] = NODE_STATE_LEAF;

    nodeId = bvh.node[nodeId].parent;
    while (atomicAdd(counter.val[nodeId], 1) > 0) {
        node = bvh.node[nodeId];
        nodeSurfaceArea = bvArea(node.bv);

        float costAsSubtree = nodeSurfaceArea * pc.c_t;
        costAsSubtree += uintBitsToFloat(sahCost.val[node.c0]);
        costAsSubtree += uintBitsToFloat(sahCost.val[node.c1]);

        float costAsLeaf = 1e69f;
        // if (node.size <= COLLAPSING_SIZE_THRESHOLD)
        if (node.size <= pc.maxLeafSize)
            costAsLeaf = nodeSurfaceArea * node.size * pc.c_i;

        if (costAsSubtree > costAsLeaf) {
            nodeState.val[nodeId] = NODE_STATE_LEAF;
            sahCost.val[nodeId] = floatBitsToUint(costAsLeaf);
        }
        else {
            nodeState.val[nodeId] = NODE_STATE_SUBTREE;
            sahCost.val[nodeId] = floatBitsToUint(costAsSubtree);
        }
        memoryBarrier(gl_ScopeDevice, gl_StorageSemanticsBuffer, gl_SemanticsAcquireRelease | gl_SemanticsMakeAvailable | gl_SemanticsMakeVisible);

        nodeId = node.parent;
        if (nodeId == INVALID_ID)
            return;
    }
}

void findCollapsedRoots(in uint32_t taskId)
{
    const uint32_t nodeId = taskId * gl_WorkGroupSize.x + gl_LocalInvocationID.x;
    if (nodeId >= pc.leafNodeCount)
        return;

    BvhBinaryDiTO14Points bvh = BvhBinaryDiTO14Points(pc.bvhAddress);
    u32Buf nodeState = u32Buf(pc.nodeStateAddress);
    u32Buf leafId = u32Buf(pc.leafNodeAddress);

    uint32_t leafNodeId = nodeId;
    uint32_t parentNodeId = bvh.node[nodeId].parent;
    uint32_t parentNodeState = nodeState.val[parentNodeId];

    while (true) {
        if (parentNodeState == NODE_STATE_LEAF)
            leafNodeId = parentNodeId;

        parentNodeId = bvh.node[parentNodeId].parent;
        if (parentNodeId == INVALID_ID)
            break;
        parentNodeState = nodeState.val[parentNodeId];
    }
    leafId.val[nodeId] = leafNodeId;
}

void invalidateCollapsedNodes(in uint32_t taskId)
{
    uint32_t nodeId = taskId * gl_WorkGroupSize.x + gl_LocalInvocationID.x;
    if (nodeId >= pc.leafNodeCount)
        return;

    BvhBinaryDiTO14Points bvh = BvhBinaryDiTO14Points(pc.bvhAddress);
    u32Buf counter = u32Buf(pc.counterAddress);
    u32Buf nodeState = u32Buf(pc.nodeStateAddress);
    u32Buf leafId = u32Buf(pc.leafNodeAddress);

    uint32_t leafNodeId = leafId.val[nodeId];
    if (nodeId == leafNodeId)
        return;
    nodeState.val[nodeId] = NODE_STATE_INVALID;

    nodeId = bvh.node[nodeId].parent;
    while (atomicAdd(counter.val[nodeId], 1) > 0) {
        if (nodeId == leafNodeId)
            return;
        nodeState.val[nodeId] = NODE_STATE_INVALID;

        memoryBarrier(gl_ScopeDevice, gl_StorageSemanticsBuffer, gl_SemanticsAcquireRelease | gl_SemanticsMakeAvailable | gl_SemanticsMakeVisible);
        nodeId = bvh.node[nodeId].parent;
        if (nodeId == INVALID_ID)
            return;
    }
}

void computeNewNodeIds(in uint32_t taskId)
{
    uint32_t nodeId = taskId * gl_WorkGroupSize.x + gl_LocalInvocationID.x;
    uint32_t totalNodeCount = 2 * pc.leafNodeCount - 1;
    if (nodeId >= totalNodeCount)
        return;

    BvhBinaryDiTO14Points bvh = BvhBinaryDiTO14Points(pc.bvhAddress);
    u32Buf nodeCounts = u32Buf(pc.collapsedTreeNodeCountsAddress);
    u32Buf nodeState = u32Buf(pc.nodeStateAddress);
    u32Buf nodeNewId = u32Buf(pc.newNodeIdAddress);

    uint32_t myState = nodeState.val[nodeId];
    uint32_t myNewOffset = 0;

    const uint32_t rootId = totalNodeCount - 1;

    uint32_t offset = 0;
    uvec4 ballot = subgroupBallot(myState == NODE_STATE_SUBTREE && nodeId != rootId);
//    uvec4 ballot = subgroupBallot(myState == NODE_STATE_SUBTREE);
    if (subgroupElect())
        offset = atomicAdd(nodeCounts.val[0], subgroupBallotBitCount(ballot));
    offset = subgroupBroadcastFirst(offset) + subgroupBallotExclusiveBitCount(ballot);
    if (myState == NODE_STATE_SUBTREE)
        myNewOffset = offset;

    ballot = subgroupBallot(myState == NODE_STATE_LEAF);
    if (subgroupElect())
        offset = atomicAdd(nodeCounts.val[1], subgroupBallotBitCount(ballot));
    offset = subgroupBroadcastFirst(offset) + subgroupBallotExclusiveBitCount(ballot);
    if (myState == NODE_STATE_LEAF)
        myNewOffset = offset;

    // root id is set during the collapse() func
    if (nodeId == rootId)
        myNewOffset = INVALID_ID;

    nodeNewId.val[nodeId] = myNewOffset;
}

void computeNewTriangleOffsets(in uint32_t taskId)
{
    uint32_t nodeId = taskId * gl_WorkGroupSize.x + gl_LocalInvocationID.x;
    uint32_t totalNodeCount = 2 * pc.leafNodeCount - 1;
    if (nodeId >= totalNodeCount)
        return;

    BvhBinaryDiTO14Points bvh = BvhBinaryDiTO14Points(pc.bvhAddress);
    u32Buf counter = u32Buf(pc.counterAddress);
    u32Buf nodeState = u32Buf(pc.nodeStateAddress);
    u32Buf newTriOffset = u32Buf(pc.newTriIdAddress);

    uint32_t myState = nodeState.val[nodeId];
    uint32_t mySize = 0;
    if (myState == NODE_STATE_LEAF)
        mySize = bvh.node[nodeId].size;

    // init DL data
    if (gl_LocalInvocationID.x == 0)
        sharedPrefix = 0;
    if (gl_LocalInvocationID.x < SC_subgroupSize)
        aggregatePerSubgroup[gl_LocalInvocationID.x] = 0;
    barrier();

    uint32_t inclusiveSum = subgroupInclusiveAdd(mySize);
    if (gl_SubgroupInvocationID == (SC_subgroupSize - 1))
        aggregatePerSubgroup[gl_SubgroupID] = inclusiveSum;
    barrier();

    uint32_t total = 0;
    if (gl_SubgroupID == 0) {
        uint32_t mySubgroupAggregate = aggregatePerSubgroup[gl_SubgroupInvocationID];
        total = subgroupInclusiveAdd(mySubgroupAggregate);
        // store exclusive sum
        aggregatePerSubgroup[gl_SubgroupInvocationID] = total - mySubgroupAggregate;
        // store total aggregate for this workgroup
        if (gl_SubgroupInvocationID == (SC_subgroupSize - 1))
            sharedPrefix = atomicAdd(counter.val[2], total);
    }
    barrier();

    if (myState == NODE_STATE_LEAF)
        newTriOffset.val[nodeId] = sharedPrefix + aggregatePerSubgroup[gl_SubgroupID] + inclusiveSum - mySize;
}

#include "data_scene.h"
layout (buffer_reference, scalar) buffer iOBBBuf { iOBB val[]; };
void collapse(in uint32_t taskId)
{
    uint32_t nodeId = taskId * gl_WorkGroupSize.x + gl_LocalInvocationID.x;
    uint32_t totalNodeCount = 2 * pc.leafNodeCount - 1;
    if (nodeId >= totalNodeCount)
        return;

    BvhBinaryDiTO14Points bvh = BvhBinaryDiTO14Points(pc.bvhAddress);
    i32Buf nodeCounts = i32Buf(pc.collapsedTreeNodeCountsAddress);
    u32Buf nodeState = u32Buf(pc.nodeStateAddress);
    i32Buf nodeIdNew = i32Buf(pc.newNodeIdAddress);
    i32Buf triOffsetNew = i32Buf(pc.newTriIdAddress);

    const int32_t numberOfLeaves = nodeCounts.val[1];

    uint32_t myState = nodeState.val[nodeId];
    if (myState == NODE_STATE_INVALID)
        return;

    int32_t newNodeId = nodeIdNew.val[nodeId];
    // unresolved root remap to keep root on defined (last) position
    if (nodeId == (totalNodeCount - 1))
        newNodeId = nodeCounts.val[0];

    if (myState == NODE_STATE_SUBTREE)
        newNodeId += numberOfLeaves;

    NodeBvhBinaryDiTO14Points node = bvh.node[nodeId];
    if (node.parent != INVALID_ID) {
        // TODO: root node as a parent situation here is scuffed, rework?
        int32_t newParentOffset = nodeIdNew.val[node.parent];
        if (newParentOffset == INVALID_ID)
            newParentOffset = nodeCounts.val[0];

        node.parent = newParentOffset + numberOfLeaves;
    }

    if (myState == NODE_STATE_LEAF) {
        node.c0 = triOffsetNew.val[nodeId];
        node.c1 = node.c0 + node.size;
        node.size = -node.size;
    }
    else {
        node.c0 = (nodeState.val[node.c0] == NODE_STATE_LEAF) ? -nodeIdNew.val[node.c0] : nodeIdNew.val[node.c0] + numberOfLeaves;
        node.c1 = (nodeState.val[node.c1] == NODE_STATE_LEAF) ? -nodeIdNew.val[node.c1] : nodeIdNew.val[node.c1] + numberOfLeaves;
    }

    Aabb aabb = Aabb(vec3(node.bv[0].x, node.bv[2].y, node.bv[4].z), vec3(node.bv[1].x, node.bv[3].y, node.bv[5].z));
    OBB obb = OBB(vec3(1.f, 0.f, 0.f), vec3(0.f, 1.f, 0.f), vec3(0.f, 0.f, 1.f), aabb.min, aabb.max);
    obbByDiTO14(obb, node.bv);

    BvhBinaryOBB bvhOBB = BvhBinaryOBB(pc.bvhCollapsedAddress);
    bvhOBB.node[newNodeId].bv[0][0] = float(nodeId);
//    bvhOBB.node[newNodeId].bv = obbMat43;
    bvhOBB.node[newNodeId].size   = node.size;
    bvhOBB.node[newNodeId].parent = node.parent;
    bvhOBB.node[newNodeId].c0 = node.c0;
    bvhOBB.node[newNodeId].c1 = node.c1;

    // TEMP obb storage to allow refit
    iOBBBuf obbBuf = iOBBBuf(pc.bvhAddress + nodeId * 184);
    obbBuf.val[0] = iOBB(obb.b0, obb.b1, obb.b2, floatBitsToInt(obb.min), floatBitsToInt(obb.max));
}

void copyTriangles(in uint32_t taskId)
{
    uint32_t nodeId = taskId * gl_WorkGroupSize.x + gl_LocalInvocationID.x;
    if (nodeId >= pc.leafNodeCount)
        return;

    u32Buf leafId = u32Buf(pc.leafNodeAddress);
    i32Buf triOffsetNew = i32Buf(pc.newTriIdAddress);
    BvhTriangles bvhTriangles = BvhTriangles(pc.bvhTrianglesAddress);
    BvhTriangles bvhCollapsedTriangles = BvhTriangles(pc.bvhCollapsedTrianglesAddress);
    BvhTriangleIndices bvhTriangleIndices = BvhTriangleIndices(pc.bvhTriangleIndicesAddress);
    BvhTriangleIndices bvhCollapsedTriangleIndices = BvhTriangleIndices(pc.bvhCollapsedTriangleIndicesAddress);

    uint32_t leafNodeId = leafId.val[nodeId];
    uint32_t myTriOffset = atomicAdd(triOffsetNew.val[leafNodeId], 1);
    bvhCollapsedTriangles.t[myTriOffset] = bvhTriangles.t[nodeId];
    bvhCollapsedTriangleIndices.val[myTriOffset] = bvhTriangleIndices.val[nodeId];
}

void fResetCounter(in uint32_t taskId)
{
    uint32_t nodeId = taskId * gl_WorkGroupSize.x + gl_LocalInvocationID.x;
    uint32_t totalNodeCount = 2 * pc.leafNodeCount - 1;
    if (nodeId >= totalNodeCount)
        return;

    u32Buf counter = u32Buf(pc.counterAddress);
    counter.val[nodeId] = 0;
}

#define atomicMinF(x,y) ((y >= 0.f) ? atomicMin(x, floatBitsToInt(y)) : atomicMax(x, int(floatBitsToUint(y))))
#define atomicMaxF(x,y) ((y >= 0.f) ? atomicMax(x, floatBitsToInt(y)) : atomicMin(x, int(floatBitsToUint(y))))

void refitObbTree(in uint32_t taskId)
{
    uint32_t nodeId = taskId * gl_WorkGroupSize.x + gl_LocalInvocationID.x;
    u32Buf nodeCounts = u32Buf(pc.collapsedTreeNodeCountsAddress);
    if (nodeId >= nodeCounts.val[1])
        return;

    BvhBinaryOBB bvh = BvhBinaryOBB(pc.bvhCollapsedAddress);
    BvhTriangleIndices triangleIndices = BvhTriangleIndices(pc.bvhCollapsedTriangleIndicesAddress);
    // TODO: rename debug address
    GeometryDescriptor gDesc = GeometryDescriptor(pc.debugAddress);

    int oldNodeId = int(bvh.node[nodeId].bv[0][0]);
    iOBBBuf obbBuf = iOBBBuf(pc.bvhAddress + oldNodeId * 184);
    iOBB obb = obbBuf.val[0];

    OBB fobb = OBB(vec3(obb.b0), vec3(obb.b1), vec3(obb.b2), intBitsToFloat(obb.min), intBitsToFloat(obb.max));
    vec3 fobbMin = fobb.min;
    vec3 fobbMax = fobb.max;

    int triStartId = bvh.node[nodeId].c0;
    int triCount = -bvh.node[nodeId].size;

    // max leaf node size is 15
    vec3[45] points;
    int pId = 0;
    for (int triId = triStartId; triId < triStartId + triCount; triId++) {
        BvhTriangleIndex ids = triangleIndices.val[triId];

        Geometry g = gDesc.g[ids.nodeId];
        uvec3Buf indices = uvec3Buf(g.idxAddress);
        fvec3Buf vertices = fvec3Buf(g.vtxAddress);
        uvec3 idx = indices.val[ids.triangleId];

        points[pId] = vertices.val[idx.x];
        refitObb(fobb, points[pId++]);
        points[pId] = vertices.val[idx.y];
        refitObb(fobb, points[pId++]);
        points[pId] = vertices.val[idx.z];
        refitObb(fobb, points[pId++]);
    }
    if (fobb.min.x < fobbMin.x) atomicMinF(obbBuf.val[0].min.x, fobb.min.x);
    if (fobb.min.y < fobbMin.y) atomicMinF(obbBuf.val[0].min.y, fobb.min.y);
    if (fobb.min.z < fobbMin.z) atomicMinF(obbBuf.val[0].min.z, fobb.min.z);
    if (fobb.max.x > fobbMax.x) atomicMaxF(obbBuf.val[0].max.x, fobb.max.x);
    if (fobb.max.y > fobbMax.y) atomicMaxF(obbBuf.val[0].max.y, fobb.max.y);
    if (fobb.max.z > fobbMax.z) atomicMaxF(obbBuf.val[0].max.z, fobb.max.z);

    while (bvh.node[nodeId].parent != INVALID_ID) {
        nodeId = bvh.node[nodeId].parent;
        oldNodeId = int(bvh.node[nodeId].bv[0][0]);
        obbBuf = iOBBBuf(pc.bvhAddress + oldNodeId * 184);
        obb = obbBuf.val[0];
        fobb = OBB(vec3(obb.b0), vec3(obb.b1), vec3(obb.b2), intBitsToFloat(obb.min), intBitsToFloat(obb.max));
        fobbMin = fobb.min;
        fobbMax = fobb.max;

        pId = 0;
        for (int triId = 0; triId < triCount; triId++) {
            refitObb(fobb, points[pId++]);
            refitObb(fobb, points[pId++]);
            refitObb(fobb, points[pId++]);
        }
        if (fobb.min.x < fobbMin.x) atomicMinF(obbBuf.val[0].min.x, fobb.min.x);
        if (fobb.min.y < fobbMin.y) atomicMinF(obbBuf.val[0].min.y, fobb.min.y);
        if (fobb.min.z < fobbMin.z) atomicMinF(obbBuf.val[0].min.z, fobb.min.z);
        if (fobb.max.x > fobbMax.x) atomicMaxF(obbBuf.val[0].max.x, fobb.max.x);
        if (fobb.max.y > fobbMax.y) atomicMaxF(obbBuf.val[0].max.y, fobb.max.y);
        if (fobb.max.z > fobbMax.z) atomicMaxF(obbBuf.val[0].max.z, fobb.max.z);
    }
}

void extractObbMatrix(in uint32_t taskId)
{
    uint32_t nodeId = taskId * gl_WorkGroupSize.x + gl_LocalInvocationID.x;
    u32Buf nodeCounts = u32Buf(pc.collapsedTreeNodeCountsAddress);
    if (nodeId >= ( nodeCounts.val[0] + nodeCounts.val[1] + 1))
        return;

    BvhBinaryOBB bvh = BvhBinaryOBB(pc.bvhCollapsedAddress);
    int oldNodeId = int(bvh.node[nodeId].bv[0][0]);
    iOBBBuf obbBuf = iOBBBuf(pc.bvhAddress + oldNodeId * 184);
    iOBB obb = obbBuf.val[0];

    vec3 obbMin = intBitsToFloat(obb.min);
    vec3 obbMax = intBitsToFloat(obb.max);
    vec3 obbCenter_lcs = (obbMin + obbMax) * 0.5f;

    // prevent flat obb
    vec3 obbDim = max((obbMax - obbMin) * 0.5f, vec3(0.001f));

    vec3 obbCenter_gcs = obb.b0 * obbCenter_lcs.x + obb.b1 * obbCenter_lcs.y + obb.b2 * obbCenter_lcs.z;

    mat4 r = mat4(obb.b0.x, obb.b0.y, obb.b0.z, 0.f,
    obb.b1.x, obb.b1.y, obb.b1.z, 0.f,
    obb.b2.x, obb.b2.y, obb.b2.z, 0.f,
    0.f, 0.f, 0.f, 1.f);
    mat4 s = mat4(obbDim.x * 2.f, 0.f, 0.f, 0.f,
    0.f, obbDim.y * 2.f, 0.f, 0.f,
    0.f, 0.f, obbDim.z * 2.f, 0.f,
    0.f, 0.f, 0.f, 1.f);
    mat4 t = mat4(1.f, 0.f, 0.f, 0.f,
    0.f, 1.f, 0.f, 0.f,
    0.f, 0.f, 1.f, 0.f,
    obbCenter_gcs.x, obbCenter_gcs.y, obbCenter_gcs.z, 1.f);

    mat4 obbMat = t * (r * s);
    obbMat = inverse(obbMat);

    mat4x3 obbMat43 = mat4x3(obbMat[0].xyz, obbMat[1].xyz, obbMat[2].xyz, obbMat[3].xyz);

    bvh.node[nodeId].bv = obbMat43;
}

#define COLLAPSING_1_LEAF_OR_INTERNAL 0
#define COLLAPSING_1_RESET_COUNTER 1
#define COLLAPSING_2_SUBTREE_ROOTS 2
#define COLLAPSING_2_RESET_COUNTER 3
#define COLLAPSING_3_INVALIDATE_SUBTREES 4
#define COLLAPSING_3_RESET_COUNTER 5
#define COLLAPSING_4_REMAP_IDS 6
#define COLLAPSING_4_RESET_COUNTER 7
#define COLLAPSING_5_TRIANGLE_OFFSETS 8
#define COLLAPSING_6_COLLAPSE_BVH 9
#define COLLAPSING_7_COPY_TRI_INDICES 10

#define COLLAPSING_REFIT_OBB 11
#define COLLAPSING_EXTRACT_OBB_MATRIX 12

#define COLLAPSING_DONE 420

void main()
{
    BvhBinaryDiTO14Points bvh = BvhBinaryDiTO14Points(pc.bvhAddress);
    BvhBinaryDiTO14Points bvhCollapsed = BvhBinaryDiTO14Points(pc.bvhCollapsedAddress);

    if (gl_GlobalInvocationID.x == 0) {
        i32Buf nodeCounts = i32Buf(pc.collapsedTreeNodeCountsAddress);
        nodeCounts.val[0] = 0;
        nodeCounts.val[1] = 0;

        allocTasks(gl_NumWorkGroups.x, COLLAPSING_1_LEAF_OR_INTERNAL);
    }

    while (true) {
        Task task = beginTask(gl_LocalInvocationID.x);

        switch (task.phase) {
            case COLLAPSING_1_LEAF_OR_INTERNAL:
                decideLeafOrInternal(task.id);

                if (endTask(gl_LocalInvocationID.x)) {
                    uint32_t totalNodeCount = 2 * pc.leafNodeCount - 1;
                    uint32_t newTaskCount = divCeil(totalNodeCount, gl_WorkGroupSize.x);
                    allocTasks(newTaskCount, COLLAPSING_1_RESET_COUNTER);
                }
                break;
            case COLLAPSING_1_RESET_COUNTER:
                fResetCounter(task.id);

                if (endTask(gl_LocalInvocationID.x))
                    allocTasks(gl_NumWorkGroups.x, COLLAPSING_2_SUBTREE_ROOTS);
                break;
            case COLLAPSING_2_SUBTREE_ROOTS:
                findCollapsedRoots(task.id);

                if (endTask(gl_LocalInvocationID.x))
                    allocTasks(gl_NumWorkGroups.x, COLLAPSING_3_INVALIDATE_SUBTREES);
                break;
            case COLLAPSING_3_INVALIDATE_SUBTREES:
                invalidateCollapsedNodes(task.id);

                if (endTask(gl_LocalInvocationID.x)) {
                    uint32_t totalNodeCount = 2 * pc.leafNodeCount - 1;
                    uint32_t newTaskCount = divCeil(totalNodeCount, gl_WorkGroupSize.x);
                    allocTasks(newTaskCount, COLLAPSING_4_REMAP_IDS);
                }
                break;
            case COLLAPSING_4_REMAP_IDS:
                computeNewNodeIds(task.id);

                if (endTask(gl_LocalInvocationID.x)) {
                    uint32_t totalNodeCount = 2 * pc.leafNodeCount - 1;
                    uint32_t newTaskCount = divCeil(totalNodeCount, gl_WorkGroupSize.x);
                    allocTasks(newTaskCount, COLLAPSING_5_TRIANGLE_OFFSETS);
                }
                break;
            case COLLAPSING_5_TRIANGLE_OFFSETS:
                computeNewTriangleOffsets(task.id);

                if (endTask(gl_LocalInvocationID.x)) {
                    uint32_t totalNodeCount = 2 * pc.leafNodeCount - 1;
                    uint32_t newTaskCount = divCeil(totalNodeCount, gl_WorkGroupSize.x);
                    allocTasks(newTaskCount, COLLAPSING_6_COLLAPSE_BVH);
                }
                break;
            case COLLAPSING_6_COLLAPSE_BVH:
                collapse(task.id);

                if (endTask(gl_LocalInvocationID.x))
                    allocTasks(gl_NumWorkGroups.x, COLLAPSING_7_COPY_TRI_INDICES);
                break;
            case COLLAPSING_7_COPY_TRI_INDICES:
                copyTriangles(task.id);

                if (endTask(gl_LocalInvocationID.x)) {
                    // store dispatch data for processing the collapsed bvh
                    u32Buf nodeCounts = u32Buf(pc.collapsedTreeNodeCountsAddress);
                    // u32Buf indirectDispatch = u32Buf(pc.indirectDispatchBufferAddress);
                    // indirectDispatch.val[0] = divCeil(nodeCounts.val[1], gl_WorkGroupSize.x);
                    // indirectDispatch.val[1] = 1;
                    // indirectDispatch.val[2] = 1;

                    const uint leafCount = nodeCounts.val[1];
                    allocTasks(divCeil(leafCount, gl_WorkGroupSize.x), COLLAPSING_REFIT_OBB);
//                    allocTasks(gl_NumWorkGroups.x, COLLAPSING_DONE);
                }
                break;
            case COLLAPSING_REFIT_OBB:
                refitObbTree(task.id);

                if (endTask(gl_LocalInvocationID.x)) {
                    u32Buf nodeCounts = u32Buf(pc.collapsedTreeNodeCountsAddress);
                    const uint nodeCount = nodeCounts.val[0] + nodeCounts.val[1] + 1;
                    allocTasks(divCeil(nodeCount, gl_WorkGroupSize.x), COLLAPSING_EXTRACT_OBB_MATRIX);
                }
                break;
            case COLLAPSING_EXTRACT_OBB_MATRIX:
                extractObbMatrix(task.id);

                if (endTask(gl_LocalInvocationID.x)) {
                    allocTasks(gl_NumWorkGroups.x, COLLAPSING_DONE);
                }
                break;
            case COLLAPSING_DONE:
                return;
        }
    };
}

