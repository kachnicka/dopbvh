#version 460

#extension GL_EXT_buffer_reference2: require
#extension GL_EXT_scalar_block_layout: require
#extension GL_EXT_shader_atomic_float : require
#extension GL_EXT_shader_explicit_arithmetic_types_int32: require
#extension GL_EXT_shader_explicit_arithmetic_types_int64: require

layout(local_size_x_id = 0) in;

#extension GL_GOOGLE_include_directive : enable

#define INCLUDE_FROM_SHADER
#include "data_bvh.h"
#include "data_plocpp.h"

#include "bv_dop14.glsl"

layout (push_constant, scalar) uniform uPushConstant {
    PC_BvhStats data;
} pc;

shared float satAggregate;
shared float saiAggregate;

shared float ciAggregate;

shared uint leafSizeSum;
shared uint leafSizeMin;
shared uint leafSizeMax;

void main() {
    if (gl_GlobalInvocationID.x >= (pc.data.nodeCount - 1))
        return;
    if (gl_LocalInvocationID.x == 0) {
        satAggregate = 0.f;
        saiAggregate = 0.f;
        ciAggregate = 0.f;

        leafSizeSum = 0;
        leafSizeMin = 0xFFFFFFFF;
        leafSizeMax = 0;
    }
    barrier();

    
    
    BvhBinaryDOP14 bvh = BvhBinaryDOP14(pc.data.bvhAddress);
    NodeBvhBinaryDOP14 node = bvh.node[gl_GlobalInvocationID.x];

    float nodeSA = bvArea(node.bv);
    if (node.size <= 1) {
        atomicAdd(saiAggregate, nodeSA);
        atomicAdd(ciAggregate, abs(node.size) * nodeSA);

        atomicAdd(leafSizeSum, abs(node.size));
        atomicMin(leafSizeMin, abs(node.size));
        atomicMax(leafSizeMax, abs(node.size));
    }
    else
        atomicAdd(satAggregate, nodeSA);


    barrier();
    BvhStats stats = BvhStats(pc.data.resultBufferAddress);
    if (gl_LocalInvocationID.x == 0) {
        atomicAdd(stats.sat, satAggregate / pc.data.sceneAabbSurfaceArea);
        atomicAdd(stats.sai, saiAggregate / pc.data.sceneAabbSurfaceArea);

        
        float sceneSA = bvArea(bvh.node[pc.data.nodeCount - 1].bv);

        atomicAdd(stats.ct, pc.data.c_t * satAggregate / pc.data.sceneAabbSurfaceArea);
        atomicAdd(stats.ci, pc.data.c_i * ciAggregate / pc.data.sceneAabbSurfaceArea);

        atomicAdd(stats.leafSizeSum, leafSizeSum);
        atomicMin(stats.leafSizeMin, leafSizeMin);
        atomicMax(stats.leafSizeMax, leafSizeMax);
    }
}
